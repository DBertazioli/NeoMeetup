= Lista di query eseguite con successo

* Distribuzione temporale eventi (time non nullo)

[source, cypher]
----
//match (e:Event)
//set e.newtime = apoc.date.format(e.time, 'ms', 'dd/MM/yyyy HH:mm:ss','GMT0:00'), conversione necessaria prima della nuova implementazione per le TZ

match (g:Group)-[r:HOSTED_EVENT]->(e:Event)
where e.time is not null
return e.name as name, e.time as time, g.country as country, g.state as state, g.lon as lon, g.lat as lat
order by name, time desc
----

* Eventi con venues e numerosità partecipanti (response yes)

[source, cypher]
----
match (e:Event) <- [r:WILL_PARTECIPATES] - (m:Member),
(e) - [h:HOSTED_AT] -> (v:Venue)
where r.response = "yes"
return e.name as Evento, count (m) as Partecipanti, v.name as Incontro
order by Partecipanti desc
----

* Gruppi per Paese

[source, cypher]
----
match (g:Group)
return distinct g.country as Country, count(g) as Numero_gruppi
order by Numero_gruppi desc
----

* Gruppi per Stati USA

[source, cypher]
----
match (g:Group)
where g.state <>  "NONE" and g.country = "us"
return distinct g.state as State, count(g) as Numero_gruppi
order by Numero_gruppi desc
----

* Media degli ospiti

[source, cypher]
----
match (m:Member)-[r:WILL_PARTECIPATES]->(e:Event)
with count(m) as Partecipanti, r.response as risposta, e.name as Evento, r.guests as Ospiti
where r.response = "yes" and Partecipanti > 10
//set r.guests = toInteger(r.guests)
return  Evento , avg(Ospiti) as AVG_Guests, Partecipanti
order by AVG_Guests desc
----

* Trend topic tra gli utenti

[source, cypher]
----
match (m:Member)-[r:IS_INTERESTED_IN]->(t:Topic)
return distinct t.name as Topic, count(m) as Totale_utenti
order by Totale_utenti desc
----

* Trend topic tra i gruppi

[source, cypher]
----
match (g:Group)-[r:DEALS_WITH]->(t:Topic)
return distinct t.name as Topic, count(g) as Totale_gruppi
order by Totale_gruppi desc
----

* Location più utilizzate per gli eventi (con localizzazione)
[source, cypher]
----
match (v:Venue)<-[r:HOSTED_AT]-(e:Event)
return v.name as Nome_ritrovo, v.lat as Latitudine, v.lon as Longitudine, count(r) as Quantità_eventi
order by Quantità_eventi desc
----

* Numero di eventi per Paese

[source, cypher]
----
match (e:Event)<-[r:HOSTED_EVENT]-(g:Group)
return count(e) as Numero_eventi,g.country as Paese
order by Numero_eventi desc
----

* Numerosità gruppi

[source, cypher]
----
match (m:Member)-[r:MEMBER_OF]->(g:Group)
return count(m) as Membri, g.name as Nome_gruppo, g.lat as Latitudine, g.lon as Longitudine, g.country as Paese, g.city as Citt�
order by Membri desc
----

* Numerosità eventi organizzati dai singoli gruppi

[source, cypher]
----
match (g:Group)-[r:HOSTED_EVENT]->(e:Event)
return g.name as Nome_gruppo, g.country as Paese, g.city as Città, g.lat as Latitudine, g.lon as Longitudine, count(e) as Numero_eventi_organizzati
order by Numero_eventi_organizzati desc
----


* Topic eventi numerosi (response=yes)

[source, cypher]
----
match (t:Topic)<-[:DEALS_WITH ]-(g:Group)-[:HOSTED_EVENT]-(e:Event)<-[will:WILL_PARTECIPATES]-(m:Member)
where will.response='yes'
return  e.id, e.name, collect(distinct t.name) as topic_list ,g.name, g.id, count(distinct m) as partecipants_count
order by partecipants_count desc
----


* Massimo numero di partecipanti ad un evento per Paese

[source, cypher]
----
match (m:Member)-[r:WILL_PARTECIPATES]->(e:Event)<-[r1:HOSTED_EVENT]-(g:Group)
with count(m) as part, e, g
return max(part) as max_part, g.country
order by max_part desc
----


* Similarity measure between declared and group topics for every member

[source, cypher]
----
match (m:Member)-[r:IS_INTERESTED_IN]->(t:Topic)<-[r1:DECLARED_INTEREST_IN]-(m)
with count(t) as corrispondenze, m
match (m)-[r:DECLARED_INTEREST_IN]->(t:Topic)
with count(r) as dichiarati, corrispondenze, m
match (m)-[r1:IS_INTERESTED_IN]-(t:Topic)
with m, corrispondenze, dichiarati, count(r1) as interessato, toFloat(corrispondenze)/toFloat(count(r1)) as score1, toFloat(corrispondenze)/toFloat(dichiarati) as score2
return m.id as ID, corrispondenze, dichiarati, interessato, score1, score2, (score1 + score2)/2 as avg_score
order by avg_score desc
----


* Similarity measure between declared and group topics for every member (with lat/lon coordinates)
[source, cypher]
----
match (m:Member)-[r:IS_INTERESTED_IN]->(t:Topic)<-[r1:DECLARED_INTEREST_IN]-(m)
where m.lat is not null
with count(t) as corrispondenze, m
match (m)-[r:DECLARED_INTEREST_IN]->(t:Topic)
where m.lat is not null
with count(r) as dichiarati, corrispondenze, m
match (m)-[r1:IS_INTERESTED_IN]-(t:Topic)
where m.lat is not null
with m, corrispondenze, dichiarati, count(r1) as interessato, toFloat(corrispondenze)/toFloat(count(r1)) as score1, toFloat(corrispondenze)/toFloat(dichiarati) as score2
return m.id as ID, m.lat as Lat, m.long as Lon, corrispondenze, dichiarati, interessato, score1, score2, (score1 + score2)/2 as avg_score
order by avg_score desc
----


* Jaccard similarity measure between declared and group topics for every member (with lat/lon coordinates)
[source, cypher]
----
match (m:Member)-[r:IS_INTERESTED_IN]->(t:Topic)<-[r1:DECLARED_INTEREST_IN]-(m)
where m.lat is not null
with count(t) as corrispondenze, m
match (m)-[r:DECLARED_INTEREST_IN]->(t:Topic)
where m.lat is not null
with count(r) as dichiarati, corrispondenze, m
match (m)-[r1:IS_INTERESTED_IN]-(t:Topic)
where m.lat is not null
with m, corrispondenze, dichiarati, count(r1) as interessato
return m.id as ID, m.lat as Lat, m.long as Lon, corrispondenze, dichiarati, interessato, (toFloat(corrispondenze))/((toFloat(dichiarati)+toFloat(interessato))-toFloat(corrispondenze)) as jac_similarity
order by jac_similarity desc
----

* Jaccard similarity measure between declared and group topics for every member
[source, cypher]
----
match (m:Member)-[r:IS_INTERESTED_IN]->(t:Topic)<-[r1:DECLARED_INTEREST_IN]-(m)
with count(t) as corrispondenze, m
match (m)-[r:DECLARED_INTEREST_IN]->(t:Topic)
with count(r) as dichiarati, corrispondenze, m
match (m)-[r1:IS_INTERESTED_IN]-(t:Topic)
with m, corrispondenze, dichiarati, count(r1) as interessato
return m.id as ID, corrispondenze, dichiarati, interessato, (toFloat(corrispondenze))/((toFloat(dichiarati)+toFloat(interessato))-toFloat(corrispondenze)) as jac_similarity
order by jac_similarity desc
----

* Distance_member_event
[source, cypher]
----
match (m:Member)-[r1:WILL_PARTECIPATES]->(e:Event)<-[r:HOSTED_EVENT]-(g:Group)
where g.lon <> 'NONE' and g.lat <> 'NONE' and m.lat is not null and m.long is not null and r1.response = 'yes'
with point({latitude: toFloat(g.lat),longitude:toFloat(g.lon)}) as p1, point({latitude:toFloat(m.lat), longitude:toFloat(m.long)}) as p2, m.id as member_id, e.id as event_id, m.lat as lat_member, m.long as lon_member, g.lat as lat_event, g.lon as lon_event
return member_id, event_id, lat_member, lon_member, lat_event, lon_event, (max(distance(p1,p2)))/1000 as dist
order by dist desc
----


* Distance_member_event_milan
[source, cypher]
----
match (m:Member)-[r:WILL_PARTECIPATES]->(e:Event)<-[r1:HOSTED_EVENT]-(g:Group)
where g.city = "Milano" and g.lon <> 'NONE' and g.lat <> 'NONE' and m.lat is not null and m.long is not null and r.response = 'yes'
optional match (g)-[r1]->(e)-[r2:HOSTED_AT]->(v:Venue)
where g.city = "Milano" and v.lat is not null and v.lon is not null
with point({latitude: toFloat(g.lat),longitude:toFloat(g.lon)}) as p1, point({latitude:toFloat(m.lat), longitude:toFloat(m.long)}) as p2, e.id as ID_event, e.name as event_name,g.lat as g_lat, g.lon as g_lon,v.name as venue_name, v.lat as v_lat, v.lon as v_lon, m.id as ID_member
return ID_member, ID_event, event_name, g_lat, g_lon, venue_name, v_lat, v_lon, (distance(p1,p2))/1000 as dist
order by ID_member desc
----